ometa KrasotaJSParser {

    spaces = < space* >:c -> [#spaces, c],
    spacesNoNl = < (~'\n' space)* >:c -> [#spaces, c],
    spacesUntil :u = < spacesNoNl '\n' >:s &apply(u) -> [#spaces, s]
        | &apply(u) -> [#spaces, '']
        | spaces,

    comment = (fromTo('//', '\n') | fromTo('/*', '*/')):c -> [#comment, c],

    sc = (comment | (< space+ >:s -> [#spaces, s]))*:c -> [#spacesAndComments, c],
    scNoNl = (comment | (< (~'\n' space)+ >:s -> [#spaces, s]))*:c -> [#spacesAndComments, c],

    commaList :t = apply(t):x (',' apply(t))*:xs -> [#commaList, x].concat(xs),

    nameFirst = letter | '$' | '_',
    nameRest = nameFirst | digit,
    iName = firstAndRest(#nameFirst, #nameRest):r -> r.join(''),
    isKeyword :x = ?KrasotaJSParser._isKeyword(x),
    name = iName:n ~isKeyword(n) -> [#name, n],
    keyword :k = iName:kk isKeyword(kk) ?(!k || k == kk) -> [#keyword, kk],

    hexDigit = char:x {this.hexDigits.indexOf(x.toLowerCase())}:v ?(v >= 0) -> v,
    hexLit = hexLit:n hexDigit:d -> (n * 16 + d)
        | hexDigit,
    number = ``0x'' hexLit:n -> [#number, n]
        | < digit+ (
            ( (('e' | 'E') ('-' | '+')?)
            | '.' )
                digit+)? >:f -> [#number, f],

    escapeChar = '\\' char:c -> [#escapeChar, c, unescape('\\' + c)],
    string = string_("'") | string_('"'),
    string_ :q = seq(q) (escapeChar | ~seq(q) char)*:cs seq(q) -> [#string, q, cs],

    op :o = sc:sc1 seq(o):c sc:sc2 -> [#op, sc1, o, sc2],

    funcRest = '(' (commaList(#funcArg) | sc):as ')' sc:s block:c -> [as, s, c],
    funcArg = sc:sc1 name:n sc:sc2 -> [#funcArg, sc1, n, sc2],

    expr = commaExpr,
    commaExpr = commaExpr:e1 op(','):o asgnExpr:e2 -> [#binop, e1, o, e2]
        | asgnExpr,
    asgnExpr = condExpr:e1 (
        ( op('=')
        | op('+=')
        | op('-=')
        | op('*=')
        | op('/=')
        | op('%=')
        | op('<<=')
        | op('>>=')
        | op('>>>=')
        | op('&=')
        | op('^=')
        | op('|=')
            ):o asgnExpr:e2 -> [#binop, e1, o, e2]
        | empty -> e1 ),
    condExpr = orExpr:e1 (
        op('?'):o1 condExpr:t op(':'):o2 condExpr:f -> [#condExpr, e1, o1, t, o2, f]
        | empty -> e1 ),
    orExpr = orExpr:e1 op('||'):o andExpr:e2 -> [#binop, e1, o, e2]
        | andExpr,
    andExpr = andExpr:e1 op('&&'):o bitOrExpr:e2 -> [#binop, e1, o, e2]
        | bitOrExpr,
    bitOrExpr = bitOrExpr:e1 op('|'):o bitXorExpr:e2 -> [#binop, e1, o, e2]
        | bitXorExpr,
    bitXorExpr = bitXorExpr:e1 op('^'):o bitAndExpr:e2 -> [#binop, e1, o, e2]
        | bitAndExpr,
    bitAndExpr = bitAndExpr:e1 op('&'):o eqExpr:e2 -> [#binop, e1, o, e2]
        | eqExpr,
    eqExpr = eqExpr:e1 (
        (op('===') | op('!==') | op('==') | op('!=')):o
            relExpr:e2 -> [#binop, e1, o, e2] )
        | relExpr,
    relExpr = relExpr:e1 (
        (op('>=') | op('>') | op('<=') | op('<') | op('instanceof') | op('in')):o
            relExpr:e2 -> [#binop, e1, o, e2] )
        | bitShiftExpr,
    bitShiftExpr = bitShiftExpr:e1 (
        (op('>>>') | op('>>') | op('<<')):o
            bitShiftExpr:e2 -> [#binop, e1, o, e2] )
        | addExpr,
    addExpr = addExpr:e1 (
        (op('+') | op('-')):o
            mulExpr:e2 -> [#binop, e1, o, e2] )
        | mulExpr,
    mulExpr = mulExpr:e1 (
        (op('*') | op('/') | op('%')):o
            unary:e2 -> [#binop, e1, o, e2] )
        | unary,
    unary = ( (op('--') | op('++') | op('+') | op('-') | op('~')):o postfix:e
            | (op('!') | op('void') | op('delete') | op('typeof')):o unary:e ) -> [#unop, o, e]
        | postfix,
    postfix = leftExpr:e
        ( scNoNl:s (seq('++') | seq('--')):o -> [#postop, e, s, o]
        | empty -> e ),
    leftExpr = callExpr | newExpr,
    callExpr = getExpr_(#callExpr)
        | callExpr:e sc:sc1 args:as -> [#callExpr, e, sc1, as]
        | getExpr,
    args = '(' (commaList(#arg) | sc):as ')' -> as,
    arg = sc:sc1 expr:e sc:sc2 -> [#arg, sc1, e, sc2],
    newExpr = keyword(#new) sc:sc1 newExpr:e -> [#newExpr, sc1, e]
        | getExpr,
    getExpr_ :e = apply(e):e sc:sc1
            ( '[' sc:sc2 expr:i sc:sc3 ']' -> [#getExprSB, e, sc1, sc2, i, sc3]
            | '.' sc:sc2 name:n -> [#getExprDot, e, sc1, sc2, n] ),
    getExpr = getExpr_(#getExpr)
        |
            ( primExpr
            | funcExpr
            | keyword(#new) sc:sc1 getExpr:e sc:sc2 args:as  -> [#newExpr, sc1, e, sc2, as] ),
    primExpr = keyword(#this)
        | name
        | number
        | string
        | arr
        | obj
        | re
        | bracketedExpr,

    bracketedExpr = '(' sc:sc1 expr:e sc:sc2 ')' -> [#bracketedExpr, sc1, e, sc2],

    funcExpr = keyword(#function) sc:sc1
        (name:n sc:sc2)?
            funcRest:c -> [#funcExpr, sc1].concat(n ? [n, sc2] : []).concat(c),

    arr = '[' (commaList(#arrItem) | sc):c ']' -> [#arr, c],
    arrItem = sc:sc1 asgnExpr:e sc:sc2 -> [#arrItem, sc1, e, sc2],

    obj = '{' (commaList(#objItem) | sc):c '}' -> [#obj, c],
    objItem = sc:sc1 (name | string):n sc:sc2
        ':' sc:sc3 asgnExpr:v sc:sc4 -> [#objItem, sc1, n, sc2, sc3, v, sc4],

    re = string_('/'):c
        ('g' | 'i' | 'm')*:f -> [#re, c, f],

    block = '{' (stmt+ | sc:s -> [s]):c '}' -> [#blockStmt, c],

    stmtWithBlockStmt :k = keyword(k) sc:sc1 block:c -> [k + #Stmt, sc1, c],
    stmtWithExprAndStmt :k = keyword(k) sc:sc1 bracketedExpr:be sc:sc2 stmtContent:c -> [k + #Stmt, sc1, be, sc2, c],
    stmtWithLabel :k = keyword(k) sc:sc1 name:l stmtEnd:se -> [k + #Stmt, sc1, l, se]
        | keyword(k) stmtEnd:se -> [k + #Stmt, se],

    stmtEnd = scNoNl:s ('\n' | ';'):c -> [#stmtEnd, s, c]
        | scNoNl:s ( &'}' | end) -> [#stmtEnd, s],

    stmt = spaces:s1 stmtContent:c spacesUntil(#stmt):s2 -> [#stmt, s1, c, s2],

    stmtContent = commentStmt
        | funcStmt
        | block

        | varStmt

        | ifStmt

        | whileStmt
        | doStmt

        | forStmt
        | forInStmt

        | breakStmt
        | continueStmt

        | switchStmt

        | throwStmt
        | tryStmt

        | returnStmt

        | withStmt

        | labelStmt

        | exprStmt

        | emptyStmt,


    commentStmt = comment:c -> [#commentStmt, c],

    funcStmt = keyword(#function) sc:sc1
        name:n sc:sc2
            funcRest:c -> [#funcStmt, sc1, n, sc2].concat(c),

    varStmt = keyword(#var) commaList(#varItem):c -> [#varStmt, c],
    varItem = varItemAsgn | varItemName,
    varItemAsgn = varItemName:n '=' sc:sc1 asgnExpr:e sc:sc2 -> [#varItemAsgn, n, sc1, e, sc2],
    varItemName = sc:sc1 name:n sc:sc2 -> [#varItemName, sc1, n, sc2],

    ifStmt = stmtWithExprAndStmt(#if):i sc:sc1 keyword(#else) sc:sc2 stmtContent:c -> i.concat([sc1, sc2, c]),
    ifStmt = stmtWithExprAndStmt(#if),

    whileStmt = stmtWithExprAndStmt(#while),
    doStmt = stmtWithBlockStmt(#do):d sc:sc1
        keyword(#while) sc:sc2 bracketedExpr:be -> d.concat([sc1, sc2, be]),

    forStmt = keyword(#for) sc:sc1 forStmtExprs:e sc:sc2 block:c -> [#forStmt, sc1, e, sc2, c],
    forStmtExprs = '(' (forStmtExpr1 | forStmtExpr):e1 ';' forStmtExpr:e2 ';' forStmtExpr:e3 ')' -> [e1, e2, e3],
    forStmtExpr1 = sc:sc1 varStmt:c sc:sc2 -> [#forStmtExpr, sc1, c, sc2],
    forStmtExpr = sc:sc1 expr:c sc:sc2 -> [#forStmtExpr, sc1, c, sc2]
        | sc:sc1 -> [#forStmtExpr, sc1],

    forInStmt = keyword(#for) sc:sc1 forInStmtExpr:e sc:sc2 block:c -> [#forInStmt, sc1, e, sc2, c],
    forInStmtExpr = '(' sc:sc1 (forInStmtVar | name):e1 sc:sc2 keyword('in') sc:sc3 asgnExpr:e2 sc:sc4 ')' -> [sc1, e1, sc2, sc3, e2, sc4],
    forInStmtVar = keyword(#var) varItemName:c -> [#varStmt, [#commaList, c]],

    breakStmt = stmtWithLabel(#break),
    continueStmt = stmtWithLabel(#continue),

    switchStmt = keyword(#switch) sc:sc1 bracketedExpr:be sc:sc2 switchStmtContent:c -> [#switchStmt, sc1, be, sc2, c],
    switchStmtContent = '{' switchStmtItem*:c '}' -> [#switchStmtContent, c],
    switchStmtItem = sc:sc1 (switchStmtCase | switchStmtDefault):l sc:sc2 ':' sc:sc3 stmt*:c -> [#switchStmtItem, sc1, l, sc2, sc3, c],

    switchStmtCase = keyword(#case) sc:s expr:e -> [#switchStmtCase, s, e],
    switchStmtDefault = keyword(#default) sc:s -> [#switchStmtDefault, s],

    throwStmt = keyword(#throw) sc:sc1 expr:e sc:sc2 stmtEnd:se -> [#throwStmt, sc1, e, sc2, se],
    tryStmt = stmtWithBlockStmt(#try):t sc:sc1 tryStmtCatch:c sc:sc2 tryStmtFinally:f sc:sc3 -> t.concat([sc1, c, sc2, f, sc3])
        | stmtWithBlockStmt(#try):t sc:sc1 tryStmtCatch:c -> t.concat([sc1, c]),
    tryStmtCatch = keyword(#catch) sc:sc1
        '(' sc:sc2 name:n sc:sc3 ')'
            sc:sc4 block:c -> [#tryStmtCatch, sc1, [#bracketedExpr, sc2, n, sc3], sc4, c],
    tryStmtFinally = keyword(#finally) sc:sc1 block:c -> [#tryStmtFinally, sc1, c],

    returnStmt = keyword(#return) (sc:sc1 expr:e)? sc:sc2 stmtEnd:se -> [#returnStmt].concat((e ? [sc1, e] : []).concat([sc2, se])),

    withStmt = stmtWithExprAndStmt(#with),

    labelStmt = name:l sc:sc1 ':' sc:sc2 stmtContent:c -> [#labelStmt, l, sc1, sc2, c],

    exprStmt = expr:e stmtEnd:se -> [#exprStmt, e, se],

    emptyStmt = ';' -> [#emptyStmt],


    topLevel = stmt*:c end -> c
}

KrasotaJSParser.hexDigits = '0123456789abcdef'

KrasotaJSParser._isKeyword = (function(ks, k) {
    var keywords = {}
    while(k = ks.shift()) keywords[k] = true
    return function(k) { return keywords.hasOwnProperty(k) }
})([
    'break', 'case', 'catch', 'continue', 'default', 'delete', 'do', 'else', 'finally', 'for', 'function', 'if', 'in',
    'instanceof', 'new', 'return', 'switch', 'this', 'throw', 'try', 'typeof', 'var', 'void', 'while', 'with'
])
